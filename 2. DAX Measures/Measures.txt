# Meal Kits = COUNTROWS('Shipping Details')

# Reviews = COUNTROWS(Reviews)

# Shipments = COUNTROWS('Shipments')

Revenue = CALCULATE(SUMX(Payments,(Payments[Subtotal] * 1.07) + Payments[ShippingFee]),FILTER(Customers,Customers[ActiveCustomer]=1))

Avg Review = AVERAGE(Reviews[Review])

Avg Meal Kits Per Shipment = DIVIDE([# Meal Kits],[# Shipments],BLANK())

Avg Reviews Per Day = DIVIDE([# Reviews],DISTINCTCOUNT(Reviews[ReviewDate]),BLANK())

Avg Revenue Per Customer = DIVIDE([Revenue],COUNTROWS(Customers),BLANK())

% Revenue from Premium Plans = DIVIDE(CALCULATE([Revenue],FILTER('Subscription Plans','Subscription Plans'[ID] IN{1,3,5})),[Revenue],BLANK())

% of Total Meal Kits = DIVIDE([# Meal Kits],CALCULATE([# Meal Kits],ALL('Meal Kits'[Item])),BLANK())

Revenue YTD = CALCULATE([Revenue],DATESYTD('Calendar'[CalendarDate]))

Revenue Previous Month = IF(ISBLANK([Revenue]),BLANK(),CALCULATE([Revenue],DATEADD('Calendar'[CalendarDate],-1,MONTH)))

Revenue - MOM Delta = DIVIDE([Revenue]-[Revenue Previous Month],[Revenue Previous Month],BLANK())

Revenue - Rolling 3 Months = IF(ISBLANK([Revenue]),BLANK(),CALCULATE([Revenue],DATESINPERIOD('Calendar'[CalendarDate],MAX('Calendar'[CalendarDate]),-3,MONTH)))

# Shipments - Rolling 3 Months = IF(ISBLANK([# Shipments]),BLANK(),CALCULATE([# Shipments],DATESINPERIOD('Calendar'[CalendarDate],MAX('Calendar'[CalendarDate]),-3,MONTH)))

# Shipments - 3 Month Average = 
VAR Shipments2MonthsPrior = CALCULATE([# Shipments],DATEADD('Calendar'[CalendarDate],-2,MONTH))
RETURN
IF(ISBLANK(Shipments2MonthsPrior),BLANK(),DIVIDE([# Shipments - Rolling 3 Months],3,BLANK()))

Within SLA % = AVERAGEX(Shipments,IF(Shipments[ProcessingLag] <= Shipments[SLAStandard],1,0))

Avg Shipping Days = AVERAGE(Shipments[DaysInTransit])

State Shipping Rank = IF(ISBLANK([Avg Shipping Days]),BLANK(),RANKX(ALL(States[StateName]),[Avg Shipping Days]))

% Top 10 Meal Kits Ordered = 
VAR Top10MealKits = CALCULATE([# Meal Kits],TOPN(10,'Meal Kits',[# Meal Kits]))
RETURN
DIVIDE(Top10MealKits,[# Meal Kits],BLANK())

DynamicMeasure = SWITCH(SELECTEDVALUE(MeasureNames[MeasureName]),"Revenue",[Revenue],"# Shipments",[# Shipments],"Avg Shipping Days",[Avg Shipping Days],[Revenue])

Projected Revenue = [Revenue] * (1 + ('Annual Growth Rate'[Annual Growth Rate Value]/12))^'Number of Months'[Number of Months Value]

Target SLA = 0.9
